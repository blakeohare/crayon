/*
    Fetch information about a file.

    args[0]: disk host object
    args[1]: well-formed and sandboxed path
    args[2]: a bit mask of properties to fetch
    args[3]: output list, size is already 6 containing old data.

        output[0] -> bool: exists
        output[1] -> bool: is directory?
        output[2] -> int: file size bytes // TODO: deal with integer overflow
        output[3] -> bool: is readonly?
        output[4] -> float: file created timestamp
        output[5] -> float: file modified timestamp

    mask & 1 -> fetch size
    mask & 2 -> fetch readonly
    mask & 4 -> fetch created timestamp
    mask & 8 -> fetch modified timestamp

    directory and existence status are always fetched.
*/
Value lib_fileiocommon_fileInfo(VmContext vm, Array<Value> args) {
    int mask = (int) args[2].internalValue;
    Native._lib_fileiocommon_getFileInfo(
        lib_fileiocommon_getDiskObject(args[0]),
        (string) args[1].internalValue,
        mask,
        Core.IntBuffer16,
        Core.FloatBuffer16);

    List<Value> outputList = Core.ForceParens((ListImpl) args[3].internalValue).items;
    VmGlobals globals = vm.globals;

    // exists
    outputList[0] = buildBoolean(globals, Core.IntBuffer16[0] > 0);

    // is directory?
    outputList[1] = buildBoolean(globals, Core.IntBuffer16[1] > 0);

    // size
    if ((mask & 1) != 0) {
        outputList[2] = buildInteger(globals, Core.IntBuffer16[2]);
    }

    // readonly?
    if ((mask & 2) != 0) {
        outputList[3] = buildBoolean(globals, Core.IntBuffer16[3] > 0);
    }

    // created timestamp
    if ((mask & 4) != 0) {
        outputList[4] = buildFloat(globals, Core.FloatBuffer16[0]);
    }

    // modified timestamp
    if ((mask & 8) != 0) {
        outputList[5] = buildFloat(globals, Core.FloatBuffer16[1]);
    }

    return args[3];
}
