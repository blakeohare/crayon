string lib_xml_parseImpl(
    VmContext vm,
    string input,
    Array<int> indexPtr,
    List<Value> output,
    Dictionary<string, string> entityLookup,
    Dictionary<int, int> stringEnders) {

    if (entityLookup.Size() == 0) {
        entityLookup["amp"] = "&";
        entityLookup["lt"] = "<";
        entityLookup["gt"] = ">";
        entityLookup["quot"] = "\"";
        entityLookup["apos"] = "'";
        // TODO: consider having two lookups, this one and another for character code entities whose
        // lifetime is bound to the life of this function call. Otherwise using one cache could potentially
        // be a huge memory leak.
        // For documents containing foreign text, this could be a very good performance boost as opposed to
        // parsing each code each time.
    }

    if (stringEnders.Size() == 0) {
        stringEnders[Core.Ord(' ')] = 1;
        stringEnders[Core.Ord('"')] = 1;
        stringEnders[Core.Ord('\'')] = 1;
        stringEnders[Core.Ord('<')] = 1;
        stringEnders[Core.Ord('>')] = 1;
        stringEnders[Core.Ord('\t')] = 1;
        stringEnders[Core.Ord('\r')] = 1;
        stringEnders[Core.Ord('\n')] = 1;
        stringEnders[Core.Ord('/')] = 1;
    }

    indexPtr[0] = 0;
    lib_xml_skipWhitespace(input, indexPtr);
    // Skip the XML declaration if present. Non-Standalone is not supported and the encoding is already changed to UTF-8 if it is a Crayon string.
    // This naive implementation feels silly though. I should parse the attributes out and verify them.
    if (lib_xml_popIfPresent(input, indexPtr, "<?xml")) {
        int newBegin = input.IndexOf("?>");
        if (newBegin == -1) {
            return lib_xml_error(input, indexPtr[0] - 5, "XML Declaration is not closed.");
        }
        indexPtr[0] = (newBegin + 2);
    }
    string error = lib_xml_skipStuff(input, indexPtr);
    if (error != null) {
        return error;
    }
    error = lib_xml_parseElement(vm, input, indexPtr, output, entityLookup, stringEnders);
    if (error != null) {
        return error;
    }
    lib_xml_skipStuff(input, indexPtr);
    if (indexPtr[0] != input.Size()) {
        return lib_xml_error(input, indexPtr[0], "Unexpected text.");
    }
    return null;
}

string lib_xml_error(string xml, int index, string msg) {
    string loc = "";
    if (index < xml.Size()) {
        int line = 1;
        int col = 0;
        int i = 0;
        while (i <= index) {
            if (xml[i] == '\n') {
                line += 1;
                col = 0;
            } else {
                col += 1;
            }
            i += 1;
        }
        loc = Core.StringConcatAll(" on line ", Core.IntToString(line), ", col ", Core.IntToString(col));
    }
    return Core.StringConcatAll("XML parse error", loc, ": ", msg);
}

string lib_xml_skipStuff(string xml, Array<int> indexPtr) {
    int index = indexPtr[0] - 1;
    while (index < indexPtr[0]) {
        index = indexPtr[0];
        lib_xml_skipWhitespace(xml, indexPtr);
        string error = lib_xml_skipComment(xml, indexPtr);
        if (error != null) {
            return error;
        }
    }
    return null;
}

int lib_xml_skipWhitespace(string xml, Array<int> indexPtr) {
    int length = xml.Size();
    for (int i = indexPtr[0]; i < length; i += 1) {
        char c = xml[i];
        if (c != ' ' && c != '\t' && c != '\n' && c != '\r') {
            indexPtr[0] = i;
            return 0;
        }
    }
    indexPtr[0] = i;
    return 0;
}

string lib_xml_skipComment(string xml, Array<int> indexPtr) {
    if (lib_xml_popIfPresent(xml, indexPtr, "<!--")) {
        int i = xml.IndexOf("-->", indexPtr[0]);
        if (i == -1) {
            return lib_xml_error(xml, indexPtr[0] - 4, "Unclosed comment.");
        }
        indexPtr[0] = (i + 3);
    }
    return null;
}

bool lib_xml_isNext(string xml, Array<int> indexPtr, string value) {
    return xml.SubStringIsEqualTo(indexPtr[0], value);
}

bool lib_xml_popIfPresent(string xml, Array<int> indexPtr, string s) {
    if (xml.SubStringIsEqualTo(indexPtr[0], s)) {
        indexPtr[0] = (indexPtr[0] + s.Size());
        return true;
    }
    return false;
}

string lib_xml_parseElement(
    VmContext vm,
    string xml,
    Array<int> indexPtr,
    List<Value> output,
    Dictionary<string, string> entityLookup,
    Dictionary<int, int> stringEnders) {

    int length = xml.Size();
    List<Value> attributeKeys = new List<Value>();
    List<Value> attributeValues = new List<Value>();
    List<Value> children = new List<Value>();
    List<Value> element = new List<Value>();
    string error = null;
    if (!lib_xml_popIfPresent(xml, indexPtr, "<")) {
        return lib_xml_error(xml, indexPtr[0], "Expected: '<'");
    }
    string name = lib_xml_popName(xml, indexPtr);
    lib_xml_skipWhitespace(xml, indexPtr);
    bool hasClosingTag = true;
    while (true) {
        if (indexPtr[0] >= length) {
            return lib_xml_error(xml, length, "Unexpected EOF");
        }
        if (lib_xml_popIfPresent(xml, indexPtr, ">")) {
            break;
        }
        if (lib_xml_popIfPresent(xml, indexPtr, "/>")) {
            hasClosingTag = false;
            break;
        }
        string key = lib_xml_popName(xml, indexPtr);
        if (key.Size() == 0) {
            return lib_xml_error(xml, indexPtr[0], "Expected attribute name.");
        }
        attributeKeys.Add(buildString(vm.globals, key));
        lib_xml_skipWhitespace(xml, indexPtr);
        if (!lib_xml_popIfPresent(xml, indexPtr, "=")) {
            return lib_xml_error(xml, indexPtr[0], "Expected: '='");
        }
        lib_xml_skipWhitespace(xml, indexPtr);
        error = lib_xml_popString(vm, xml, indexPtr, attributeValues, entityLookup, stringEnders);
        if (error != null) {
            return error;
        }
        lib_xml_skipWhitespace(xml, indexPtr);
    }
    if (hasClosingTag) {
        string close = Core.StringConcatAll("</", name, ">");
        while (!lib_xml_popIfPresent(xml, indexPtr, close)) {
            if (lib_xml_isNext(xml, indexPtr, "</")) {
                error = lib_xml_error(xml, indexPtr[0] - 2, "Unexpected close tag.");
            } else {
                if (lib_xml_isNext(xml, indexPtr, "<!--")) {
                    error = lib_xml_skipComment(xml, indexPtr);
                } else {
                    if (lib_xml_isNext(xml, indexPtr, "<")) {
                        error = lib_xml_parseElement(vm, xml, indexPtr, children, entityLookup, stringEnders);
                    } else {
                        error = lib_xml_parseText(vm, xml, indexPtr, children, entityLookup);
                    }
                }
            }
            if ((error == null) && (indexPtr[0] >= length)) {
                error = lib_xml_error(xml, length, "Unexpected EOF. Unclosed tag.");
            }
            if (error != null) {
                return error;
            }
        }
    }
    element.Add(vm.globalTrue);
    element.Add(buildString(vm.globals, name));
    element.Add(buildList(attributeKeys));
    element.Add(buildList(attributeValues));
    element.Add(buildList(children));
    output.Add(buildList(element));
    return null;
}

string lib_xml_parseText(VmContext vm, string xml, Array<int> indexPtr, List<Value> output, Dictionary<string, string> entityLookup) {
    int length = xml.Size();
    int start = indexPtr[0];
    int i = start;
    bool ampFound = false;
    char c = ' ';
    while (i < length) {
        c = xml[i];
        if (c == '<') {
            break;
        } else if (c == '&') {
            ampFound = true;
        }
        i += 1;
    }
    if (i > start) {
        indexPtr[0] = i;
        string textValue = xml.SubString(start, (i - start));
        if (ampFound) {
            textValue = lib_xml_ampUnescape(textValue, entityLookup);
        }
        List<Value> textElement = new List<Value>();
        textElement.Add(vm.globalFalse);
        textElement.Add(buildString(vm.globals, textValue));
        output.Add(buildList(textElement));
    }
    return null;
}

string lib_xml_popName(string xml, Array<int> indexPtr) {
    int length = xml.Size();
    int i = indexPtr[0];
    int start = i;
    char c = ' ';
    // TODO: proper char matching based on https://www.w3.org/TR/REC-xml/#d0e804
    while (i < length) {
        c = xml[i];
        if ((c >= 'a' && c <= 'z') ||
            (c >= 'A' && c <= 'Z') ||
            (c >= '0' && c <= '9') ||
            c == '_' ||
            c == '.' ||
            c == ':' ||
            c == '-') {
            // yup, that's valid.
        } else {
            break;
        }
        i += 1;
    }
    string output = xml.SubString(start, i - start);
    indexPtr[0] = i;
    return output;
}

string lib_xml_popString(
    VmContext vm,
    string xml,
    Array<int> indexPtr,
    List<Value> attributeValueOut,
    Dictionary<string, string> entityLookup,
    Dictionary<int, int> stringEnders) {

    int length = xml.Size();
    int start = indexPtr[0];
    int end = length;
    int i = start;
    int stringType = xml.CharCodeAt(i);

    // Allow unwrapped strings. Not standard, but common enough of a mistake to support
    // Unwrapped strings are terminated by an operator character <, >, =, ", ', or space
    bool unwrapped = stringType != Core.Ord('"') && stringType != Core.Ord('\'');
    bool ampFound = false;
    int c = Core.Ord(' ');
    if (unwrapped) {
        while (i < length) {
            c = xml.CharCodeAt(i);
            if (stringEnders.Contains(c)) {
                end = i;
                break;
            } else if (c == Core.Ord('&')) {
                ampFound = true;
            }
            i += 1;
        }
    } else {
        i += 1;
        start = i;
        while (i < length) {
            c = xml.CharCodeAt(i);
            if (c == stringType) {
                end = i;
                i += 1;
                break;
            } else if (c == Core.Ord('&')) {
                ampFound = true;
            }
            i += 1;
        }
    }
    indexPtr[0] = i;
    string output = xml.SubString(start, end - start);
    if (ampFound) {
        output = lib_xml_ampUnescape(output, entityLookup);
    }
    attributeValueOut.Add(buildString(vm.globals, output));
    return null;
}

string lib_xml_ampUnescape(string value, Dictionary<string, string> entityLookup) {
    Array<string> ampParts = value.Split("&");

    for (int i = 1; i < ampParts.Size(); i += 1) {
        string component = ampParts[i];
        int semicolon = component.IndexOf(";");
        // stray &'s without a semicolon are bregudgingly preserved without error.
        // TODO: Pass in a boolean for strict mode, maybe? Or maybe that's too puritanical?
        if (semicolon != -1) {
            string entityCode = component.SubString(0, semicolon);
            string entityValue = lib_xml_getEntity(entityCode, entityLookup);
            if (entityValue == null) {
                // put the & back and just move on.
                entityValue = "&";
            } else {
                // strip out the entity code and semicolon
                component = component.SubString((semicolon + 1), (component.Size() - semicolon));
            }
            ampParts[i] = Core.StringConcatAll(entityValue, component);
        }
    }

    return ampParts.Join("");
}

string lib_xml_getEntity(string code, Dictionary<string, string> entityLookup) {
    if (entityLookup.Contains(code)) {
        return entityLookup[code];
    }

    /*
    if ($_string_char_code_at(code, 0) == $_ord('#')) {
        // TODO: add parsing for stuff like &#39; and &#x123F;
    }//*/

    return null;
}
