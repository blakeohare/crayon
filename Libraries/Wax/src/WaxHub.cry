import Json;
import Suspension;

namespace Wax {

    class WaxHub {
        @private
        constructor() { }

        static field _instance = null;

        static function getActiveHub() {
            if (WaxHub._instance == null) {
                WaxHub._instance = new WaxHub();
            }
            return WaxHub._instance;
        }

        function awaitSendRequest(serviceId, payload) {
            isDone = false;
            result = null;
            err = null;

            xlock = new ExecutionLock();
            this.sendRequest(serviceId, payload, _result => {
                xlock.unblockCallback();
                result = _result;
            }, _err => {
                xlock.unblockCallback();
                err = _err;
            });

            xlock.block();

            if (err != null) throw new Exception(err);

            return result;
        }

        function sendRequest(serviceId, payload, cb = null, errCb = null) {
            serializedData = WaxHub._serializeData(payload);
            $$$('waxSend', serviceId + '', serializedData, (err, rawData) => {
                if (err == null) {
                    if (cb != null) {
                        res = Json.parseJson(rawData);
                        cb(res);
                    }
                } else {
                    if (errCb == null) throw new Exception(err);
                    errCb(err);
                }
            }, false);
        }

        static function _serializeData(payload) {
            if (Core.typeof(payload) != Type.DICTIONARY) throw new InvalidArgumentException("Payload must be a JSON-serializable dictionary.");
            serializedData = Json.serializeJson(payload); // TODO: catch error here if there's a bad nested type
            return serializedData;
        }

        function registerListener(serviceId, payload, cb, errCb = null) {
            if (cb == null) throw new InvalidArgumentException("Callback cannot be null");
            serializedData = WaxHub._serializeData(payload);
            $$$('waxSend', serviceId + '', serializedData, (err, rawData) => {
                if (err != null) {
                    if (errCb == null) throw new Exception(err);
                    errCb(err);
                } else {
                    res = Json.parseJson(rawData);
                    cb(res);
                }
            }, true);
        }
    }
}
