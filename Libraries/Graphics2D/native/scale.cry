/*
    arg 1: Original GraphicsTexture instance
    arg 2: New GraphicsTexture instance without nativeData
        Need to copy the nativeData over.

        nativeData[0] -> weak reference to GraphicsTextureResource's nativeData
        nativeData[1] -> texture left
        nativeData[2] -> texture top
        nativeData[3] -> texture right
        nativeData[4] -> texture button
        nativeData[5] -> width
        nativeData[6] -> height

    arg 3: desired width
    arg 4: desired height
    arg 5: original GraphicsTextureResource
    arg 6: new GraphicsTextureResource.
        This needs to be initialized on non-atlas platforms.
        On GL platforms, this will be identical to the previous argument since scaling only changes the render width and height.

        nativeData[0] -> is GL?
        nativeData[1] -> texture loaded?
        nativeData[2] -> texture ID (GL only)
        nativeData[3] -> platform image
        nativeData[4] -> width
        nativeData[5] -> height

    At least one of width and height will be guaranteed to be different.

    Because atlas based texture's sizes are based on the width and height fields
    themselves, this function no-ops for those platforms.
*/
import inline "FrameworkFunctions/6_arg.cry";

int1 = $_cast('int', Value$arg3.internalValue); // New width
int2 = $_cast('int', Value$arg4.internalValue); // New height

if (!%%%LIB_GRAPHICS_USES_TEXTURE_ATLAS%%%) {

    // Get the native platform image from the original image (GraphicsTextureResource)
    objInstance1 = $_cast('ObjectInstance', Value$arg5.internalValue);
    object1 = $_array_get(ObjectInstance$objInstance1.nativeData, 3); // original image

    // New GraphicsTextureResource. Add nativeData.
    objInstance1 = $_cast('ObjectInstance', Value$arg6.internalValue);
    objArray1 = $_new_array('object', 6);
    ObjectInstance$objInstance1.nativeData = objArray1;
    $_array_set(objArray1, 0, false);
    $_array_set(objArray1, 1, true);
    $_array_set(objArray1, 2, 0);
    $_array_set(objArray1, 3, $_lib_graphics2d_scale(object1, int1, int2));
    $_array_set(objArray1, 4, int1);
    $_array_set(objArray1, 5, int2);
}

// Copy the original GraphicsTexture's native data to the new GraphicsTexture

// The new GraphicsTexture
objInstance2 = $_cast('ObjectInstance', Value$arg2.internalValue);
objArray1 = $_new_array('object', 7);
ObjectInstance$objInstance2.nativeData = objArray1;
objInstance2 = $_cast('ObjectInstance', Value$arg1.internalValue);
objArray2 = ObjectInstance$objInstance2.nativeData;
for (i = 4; i >= 1; i -= 1) { // leave off width and height since we overwrite that anyway
    $_array_set(objArray1, i, $_array_get(objArray2, i));
}

// Now change the width and height of the native data
$_array_set(objArray1, 5, int1);
$_array_set(objArray1, 6, int2);
// Now add a pointer to the new GraphicsTextureResource's native data
objInstance1 = $_cast('ObjectInstance', Value$arg6.internalValue);

$_array_set(objArray1, 0, ObjectInstance$objInstance1.nativeData);

output = arg1;
