import CrayonUnit;
import Math;

class OpsEqualityTestHelper {
    function f1() { }
    function f2() { }
    static function f3() { }
    static function f4() { }
}

class OpsTest : Test {

    constructor() : base() { }

    /*
        The actual and expected outputs are converted to strings before being compared.
        This is so that the type (float vs integer) can be verified. When a float is
        converted to a string, it will always have a decimal. Otherwise a float and
        integer of equal mathematical values will likely pass a == test.
    */
    function assertEq(expected, actual) {
        exp = this.canonicalizeNum(expected);
        act = this.canonicalizeNum(actual);
        assertThat(exp).isEqualTo(act);
    }

    function assertPowThrows(baseNum, powNum) {
        n = 0;
        try {
            n = baseNum ** powNum;
        } catch (InvalidArgumentException) {
            pass();
            return;
        }
        // cheesy but whatever
        assertThat(baseNum + ' ** ' + powNum + ' --> ' + n).isEqualTo('InvalidArgumentException');
    }

    // convert numerical values to strings to preserve the float-vs-int-ness of it.
    // For floats, trim it to 10 string characters to ensure
    function canonicalizeNum(n) {
        s = n + '';
        if (s.contains('.')) {
            s = ('' + Math.floor(n * 100000000 + .5) / 100000000.0)[:10];
        }
        return s;
    }

    function testBasics() {

        /*
            Each test runs twice. Once with the constants inline and once with the
            aptly named variables. This is because the Crayon compiler will optimize
            obvious expressions that only contain inline constants rather than compile
            it as an expression. The duplication is to exercise both of these codepaths.
        */

        one = 1;
        two = 2;
        three = 3;
        four = 4;
        one_f = 1.0;
        two_f = 2.0;
        three_f = 3.0;
        four_f = 4.0;
        neg_one = -1;
        neg_one_f = -1.0;
        neg_four = -4;
        neg_four_f = -4.0;
        half = 0.5;

        this.assertEq(1 + 1, 2);
        this.assertEq(one + one, 2);

        this.assertEq(1.0 + 1, 2.0);
        this.assertEq(one_f + one, 2.0);

        this.assertEq(1 + 1.0, 2.0);
        this.assertEq(one + one_f, 2.0);

        this.assertEq(1.0 + 1.0, 2.0);
        this.assertEq(one_f + one_f, 2.0);

        this.assertEq(1 - 1, 0);
        this.assertEq(one - one, 0);

        this.assertEq(1.0 - 1, 0.0);
        this.assertEq(one_f - one, 0.0);

        this.assertEq(1 - 1.0, 0.0);
        this.assertEq(one - one_f, 0.0);

        this.assertEq(1.0 - 1.0, 0.0);
        this.assertEq(one_f - one_f, 0.0);

        this.assertEq(2 * 3, 6);
        this.assertEq(two * three, 6);

        this.assertEq(2.0 * 3, 6.0);
        this.assertEq(two_f * three, 6.0);

        this.assertEq(2 * 3.0, 6.0);
        this.assertEq(two * three_f, 6.0);

        this.assertEq(2.0 * 3.0, 6.0);
        this.assertEq(two_f * three_f, 6.0);

        this.assertEq(4 / 2, 2);
        this.assertEq(four / two, 2);

        this.assertEq(4.0 / 2, 2.0);
        this.assertEq(four_f / two, 2.0);

        this.assertEq(4 / 2.0, 2.0);
        this.assertEq(four / two_f, 2.0);

        this.assertEq(4.0 / 2.0, 2.0);
        this.assertEq(four_f / two_f, 2.0);

        this.assertEq(3 / 2, 1);
        this.assertEq(three / two, 1);

        this.assertEq(4 ** 2, 16.0);
        this.assertEq(four ** two, 16.0);

        this.assertEq(4 ** 2.0, 16.0);
        this.assertEq(four ** two_f, 16.0);

        this.assertEq(4 ** -1, 0.25);
        this.assertEq(four ** neg_one_f, 0.25);

        this.assertEq(4.0 ** -1, 0.25);
        this.assertEq(four_f ** neg_one_f, 0.25);

        this.assertEq(3 & 2, 2);
        this.assertEq(three & two, 2);

        this.assertEq(4 | 1, 5);
        this.assertEq(four | one, 5);

        this.assertEq(3 ^ 1, 2);
        this.assertEq(three ^ one, 2);

        this.assertEq(3 << 3, 24);
        this.assertEq(three << three, 24);

        this.assertEq(4 >> 1, 2);
        this.assertEq(four >> one, 2);

        this.assertEq((-4) >> 1, -2);
        this.assertEq(neg_four >> one, -2);

        this.assertEq(4 ** 0.5, 2.0);
        this.assertEq(four ** half, 2.0);

        this.assertPowThrows(-555, 0.5);

        this.assertEq(3 % 2, 1);
        this.assertEq(three % two, 1);
        this.assertEq(neg_one % 2, 1);

        this.assertEq(3.0 % 2, 1.0);
        this.assertEq(three_f % two, 1.0);
        this.assertEq(neg_one_f % two, 1.0);

        this.assertEq(3 % 2.0, 1.0);
        this.assertEq(three % two_f, 1.0);
        this.assertEq(neg_one % two_f, 1.0);

        this.assertEq(3.0 % 2.0, 1.0);
        this.assertEq(three_f % two_f, 1.0);
        this.assertEq(neg_one_f % two_f, 1.0);
    }

    function testEquality() {
        nil1 = null;
        nil2 = null;
        int1 = 1;
        int2 = 2;
        float1 = 1.0;
        float2 = 2.0;
        string1 = '1';
        string2 = '2';
        list1 = [];
        list2 = [];
        dict1 = {};
        dict2 = {};
        fp1 = Core.ord;
        fp2 = Core.chr;
        eth = new OpsEqualityTestHelper();
        fp3 = eth.f1;
        fp4 = eth.f2;
        fp5 = OpsEqualityTestHelper.f3;
        fp6 = OpsEqualityTestHelper.f4;
        fp7 = list1.add;
        fp8 = list1.clear;
        fp9 = list2.add;
        fp10 = new OpsEqualityTestHelper().f1;
        cls1 = Core.Object.class;
        cls2 = OpsEqualityTestHelper.class;

        assertThat(nil1 == nil1).isTrue();
        assertThat(nil1 == nil2).isTrue();
        assertThat(nil1 == int1).isFalse();
        assertThat(int1 == nil1).isFalse();

        assertThat(int1 == int1).isTrue();
        assertThat(int2 == int2).isTrue();
        assertThat(int1 == int2).isFalse();

        assertThat(float1 == float1).isTrue();
        assertThat(float2 == float2).isTrue();
        assertThat(float1 == float2).isFalse();

        assertThat(string1 == string1).isTrue();
        assertThat(string2 == string2).isTrue();
        assertThat(string1 == string2).isFalse();

        assertThat(list1 == list1).isTrue();
        assertThat(list2 == list2).isTrue();
        assertThat(list1 == list2).isFalse();

        assertThat(dict1 == dict1).isTrue();
        assertThat(dict2 == dict2).isTrue();
        assertThat(dict1 == dict2).isFalse();

        assertThat(cls1 == cls1).isTrue();
        assertThat(cls2 == cls2).isTrue();
        assertThat(cls1 == cls2).isFalse();

        // identical references
        assertThat(fp1 == fp1).isTrue();
        assertThat(fp2 == fp2).isTrue();
        assertThat(fp3 == fp3).isTrue();
        assertThat(fp4 == fp4).isTrue();
        assertThat(fp5 == fp5).isTrue();
        assertThat(fp6 == fp6).isTrue();
        assertThat(fp7 == fp7).isTrue();
        assertThat(fp8 == fp8).isTrue();
        assertThat(fp9 == fp9).isTrue();

        // same type different function
        assertThat(fp1 == fp2).isFalse();
        assertThat(fp3 == fp4).isFalse();
        assertThat(fp5 == fp6).isFalse();
        assertThat(fp7 == fp8).isFalse();

        // same function, different context
        assertThat(fp3 == fp10).isFalse();
        assertThat(fp7 == fp9).isFalse();

        // different function type altogether
        assertThat(fp2 == fp3).isFalse();
        assertThat(fp4 == fp5).isFalse();
        assertThat(fp6 == fp7).isFalse();
    }
}
