import Base64;
import CrayonExporter;
import FileIO;
import FileIOTemp;
import Images;
import Json;
import Math;
import Resources;
import TextEncoding;
import Wax;

class IosExporter {

    field buildData;
    field resDb;
    field properties;
    field cbxBundle;
    field templates;
    field outputDir;
    field args;

    constructor(buildData, useTempDir, args) {
        this.buildData = buildData;
        this.cbxBundle = this.buildData.getCbxBundle();
        this.resDb = this.cbxBundle.getResourceDatabase();
        this.properties = this.buildData.getExportProperties();
        this.templates = new TemplateLoader();

        this.outputDir = useTempDir
            ? FileIOTemp.TempFiles.createDirectory(5 * 60) // temp dir exists for 5 minutes
            : this.properties.getOutputDirectory();
        this.args = args;
    }

    function getOrientation() {
        orientations = {};
        for (str : this.args.get('orientations') ?? []) {
            orientations[str.lower().trim()] = true;
        }

        return {
            '    ': 'unspecified',
            ' ^  ': 'portrait',
            '   v': 'reversePortrait',
            ' ^ v': 'sensorPortrait',
            '<   ': 'landscape',
            '  > ': 'reverseLandscape',
            '< > ': 'sensorLandscape',
            '<^>v': 'fullSensor',
        }.get([
            orientations.get('landscape', false) || orientations.get('landscapeleft', false) || orientations.get('all', false)
                ? '<' : ' ',
            orientations.get('portrait', false) || orientations.get('all', false)
                ? '^' : ' ',
            orientations.get('landscape', false) || orientations.get('landscaperight', false) || orientations.get('all', false)
                ? '>' : ' ',
            orientations.get('upsidedown', false) || orientations.get('all', false)
                ? 'v' : ' ',
        ].join(''))
            ?? 'fullSensor'; // enabling both orientations in one direction but only allowing one orientation in the transverse direction isn't a valid supported option in android, at least at the moment and as far as I can tell.
    }

    function exportJsBaseProject() {

        response = WaxHub.getActiveHub().awaitSendRequest('ExportWebExtension', {
            'buildData': this.buildData._data,
            'extArgs': {
                'useTmpDir': true,
                'filePrefix': '/',
                'fullPage': true,
                'min': false,
            }
        });

        return response;
    }

    function export() {
        errors = [];

        projectId = this.buildData.getProjectId();

        title = this.args.get('title');
        if (title.length == 0) {
            errors.add("App title is blank.");
        }

        if (errors.length > 0) {
            return { "errors": errors };
        }

        this.templates
            .addReplacement('PROJECT_ID', projectId)
            .addReplacement('PROJECT_ID_LOWERCASE', projectId.lower())
            .addReplacement('PROJECT_TITLE', title);
            //.addReplacement('ANDROID_ORIENTATION', this.getOrientation());

        response = this.exportJsBaseProject();
        errors = response.get('errors') ?? [];
        if (errors.length > 0) {
            return response;
        }
        jsDir = response.get('outputDir');
        files = {};
        includeWorkspace = !FileIO.fileExists(this.canonicalizePath(this.outputDir + '/.idea/workspace.xml'));
        this.doExport(jsDir, files, includeWorkspace);
        FileData.writeToDisk(this.outputDir, files);
    }

    function canonicalizePath(path) {
        parts = path.split('/');
        out = [parts[0]];
        for (i = 1; i < parts.length; i++) {
            if (parts[i] != '') out.add(parts[i]);
        }
        return FileIO.pathJoin(out);
    }

    function doExport(jsProjDir, filesOut, includeWorkspace) {
        projectId = this.buildData.getProjectId();

        files = {};
        for (file : this.gatherFilePaths(jsProjDir)) {
            files[file] = true;
        }
        files['test_server.py'] = false;

        jsDir = projectId + '/' + projectId + '/jsres';
        for (path : files.keys()) {
            if (files[path]) {
                absPath = this.canonicalizePath(jsProjDir + '/' + path);
                if (path.endsWith('.js') || path.endsWith('.txt') || path.endsWith('.html')) {
                    text = FileIO.fileReadText(absPath);
                    text = this.alterBaseWebFile(path, text);
                    file = FileData.createTextFile(text);
                } else {
                    bytes = FileIO.fileReadBytes(absPath);
                    file = FileData.createBinaryFileFromBytes(bytes);
                }
                filesOut[jsDir + '/' + path] = file;
            }
        }

        prefix = projectId + '/' + projectId + '/';
        filesOut[jsDir + '/ios.js'] = FileData.createTextFile(this.templates.getText('ios.js'));
        filesOut[projectId + '/' + projectId + '.xcodeproj/project.pbxproj'] = this.getTextFile('project.pbxproj');

        simpleCopies = {
            'AppDelegate.swift': 'AppDelegate.swift',
            'Assets.xcassets/AppIcon.appiconset/Contents.json': 'IconSetContent.json',
            'Assets.xcassets/Launchscreen.imageset/Contents.json': 'ImageSetContent.json',
            'Base.lproj/LaunchScreen.storyboard': 'LaunchScreen.storyboard',
            'Base.lproj/Main.storyboard': 'Main.storyboard',
            'Info.plist': 'Info.plist',
            'ViewController.swift': 'ViewController.swift',
            'jsres/ios.js': 'ios.js',
        };
        for (simpleCopyTarget : simpleCopies.keys()) {
            simpleCopySource = simpleCopies[simpleCopyTarget];
            filesOut[prefix + simpleCopyTarget] = this.getTextFile(simpleCopySource);
        }

        this.exportIcons(prefix, filesOut);
    }

    function exportIcons(prefix, filesOut) {

        // Export icons for each of the various DPI's by using the provided icons and figuring out what the best fit is, size-wise.

        icons = [];
        for (iconPath : this.properties.getIconPaths()) {
            icons.add(Bitmap.fromBytes(FileIO.fileReadBytes(iconPath)));
        }
        if (icons.length == 0) throw new Exception();

        for (icon : this.iconBatchResizer(icons, [
            20 * 1,
            20 * 2,
            20 * 3,
            29 * 1,
            29 * 2,
            29 * 3,
            40 * 1,
            40 * 2,
            40 * 3,
            60 * 2,
            60 * 3,
            76 * 1,
            76 * 2,
            167, // 83.5 * 2,
            1024,
        ])) {
            filesOut[prefix + '/Assets.xcassets/AppIcon.appiconset/icon' + icon.width + '.png'] = FileData.createBinaryFileFromBytes(icon.toBytes(ImageFormat.PNG));
        }
    }

    // TODO: refactor this to make it shared between Android and iOS exporters.
    function iconBatchResizer(originalIcons, requiredSizes) {

        if (originalIcons.length == 0) throw new Exception("Must provide at least one icon");

        largest = originalIcons[0];
        smallest = originalIcons[0];
        for (i = 1; i < originalIcons.length; i++) {
            icon = originalIcons[i];
            if (Math.max(icon.width, icon.height) > Math.max(largest.width, largest.height)) largest = icon;
            if (Math.max(icon.width, icon.height) < Math.max(smallest.width, smallest.height)) smallest = icon;
        }

        unique = {};
        for (dim : requiredSizes) {
            unique[dim] = true;
        }
        dimensions = unique.keys();
        dimensions.sort();

        output = [];

        for (size : dimensions) {
            icon = null;
            if (size > Math.max(largest.width, largest.height)) icon = largest;
            else if (size < Math.max(largest.width, largest.height)) icon = smallest;
            else {
                best = largest;
                // find the smallest icon that is larger than the given size
                for (candidate : originalIcons) {
                    cSz = Math.max(candidate.width, candidate.height);
                    if (cSz >= size && cSz < Math.max(best.width, best.height)) {
                        best = candidate;
                    }
                }
                icon = best;
            }

            iconCopy = new Bitmap(size, size);
            if (icon.width == icon.height) {
                newWidth = size;
                newHeight = size;
            } else if (icon.width > icon.height) {
                newHeight = size * icon.height / icon.width;
                newWidth = size;
            } else {
                newWidth = size * icon.width / icon.height;
                newHeight = size;
            }
            iconCopy
                .edit()
                .drawImageStretched(icon, (size - newWidth) / 2, (size - newHeight) / 2, newWidth, newHeight)
                .finish();
            output.add(iconCopy);
        }

        return output;
    }

    function getTextFile(path) {
        return FileData.createTextFile(this.templates.getText(path));
    }

    function gatherFilePaths(dir) {
        out = [];
        this.gatherFilePathsImpl(dir, '', out);
        return out;
    }

    function alterBaseWebFile(path, content) {
        switch (path) {
            case 'index.html': return this.modifyIndexHtml(content);
            default: return content;
        }
    }

    function modifyIndexHtml(code) {
        lines = code.split('\n');
        newLines = [];
        for (line : lines) {
            if (line.contains('favicon.ico')) continue;
            if (line.trim() == '</head>') {
                newLines.add(
                    "<script src=\"ios.js\"></script>",
                    "<style type=\"text/css\">",
                    "  body { margin:0px; background-color:#000; }",
                    "  #crayon_host {",
                    "    background-color:#000;",
                    "    text-align:left;",
                    "    width:100%;",
                    "    height:100%;",
                    "  }",
                    "</style>");
            }
            newLines.add(line);
        }

        code = newLines.join('\n');
        /*
        code = code.replace(
            '<script src="',
            '<script src="file:///android_asset/');
        */
        return code;
    }

    function gatherFilePathsImpl(dir, prefix, out) {
        for (file : FileIO.directoryList(this.canonicalizePath(dir))) {
            skip = false;
            switch (file.lower()) {
                case '.ds_store':
                case 'thumbs.db':
                case '.gitignore':
                    skip = true;
                    break;
            }

            if (skip) continue;

            absPath = dir + '/' + file;
            relPath = prefix + (prefix == '' ? '' : '/') + file;

            if (FileIO.directoryExists(this.canonicalizePath(absPath))) {
                this.gatherFilePathsImpl(absPath, relPath, out);
            } else {
                out.add(relPath);
            }
        }
    }
}
