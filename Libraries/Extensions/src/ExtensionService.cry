import Json;

namespace Extensions {

    function main(args) {
        expectedName = Core.getProjectId() + "Service";
        invoked = false;
        for (cls : Core.Reflection.getClasses(ExtensionService.class)) {
            if (cls.getName().split('.')[-1] == expectedName) {
                service = cls.createInstance();
                // TODO: add a way to use magic debug args to override this for testing.
                payload = $$$('waxServiceGetPayload');
                if (payload == '') {
                    print("ERROR: Extensions cannot be run directly and must be invoked from the Crayon Toolchain.");
                    return;
                }
                extArgs = Json.parseJson(payload);
                service._extensionArgBuildData = extArgs.get('buildData');
                service._extensionArgCbxPath = extArgs.get('cbxPath');
                service.handleRequest(extArgs.get('extArgs'), res => {
                    if (invoked) return;
                    invoked = true;
                    res = res ?? {};
                    if (Core.typeof(res) != Type.DICTIONARY) {
                        err = "ExtensionService response must be a dictionary.";
                        throw new InvalidArgumentException(err);
                    }
                    json = Json.serializeJson(res);
                    $$$('waxServiceSendResponse', json);
                });
                return;
            }
        }
        err = "A class with the name '" + expectedName + "' that extends from Extensions.ExtensionService is required.";
        throw new Exception(err);
    }

    class ExtensionService {

        field _extensionArgBuildData;
        function getBuildData() { return this._extensionArgBuildData; }

        field _extensionArgCbxPath;
        function getCbxPath() { return this._extensionArgCbxPath; }

        function handleRequest(payload, cb) {
            err = "ExtensionService.handleRequest(payload, cb) must be overridden";
            throw new NotImplementedException(err);
        }
    }
}
